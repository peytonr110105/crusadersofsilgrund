#Behaviors for new monsters

on damage of monster:
	#Generic DR Behavior
	if victim is monster:
		if victim is alive:
			clear {_damageTacticChosen}
			if {t_lvl.%victim%} is more than 30:
				set {_mobLVLScalar} to 30
			else:
				set {_mobLVLScalar} to {t_lvl.%victim%}
			set {_drOdds} to floor(({t_spd.%victim%} / 2))
			if {_drOdds} is more than 60:
				set {_drOdds} to 60
			set {_drOdds} to floor(({_drOdds} - 10) + ({_mobLVLScalar} / 2))
			if ({_drOdds} - 10) is more than 0:
				#All mobs can occasionally dodge roll away from the player
				chance of (({_drOdds} - 20) + (10 * getEntityWeaponType(victim)))%:
					if {t_foeDRTimeout.%victim%} is not set:
						chance of 50%:
							wait 0.1 seconds
						set {_damageTacticChosen} to true
						push the victim in horizontal direction of the victim at speed -1.35
						push the victim up at speed 0.3
						set {t_foeDRTimeout.%victim%} to true
						set {t_tangibilityState.%victim%} to "DRInvulnerable"
						loop 9 times:
							play 5 (smoke with speed 0) at the victim
							play 5 (cloud with speed 0) at the victim
							wait 1 tick
						clear {t_tangibilityState.%victim%}
						play sound "block.gravel.break" with volume 0.7 and pitch 0.5 at the victim
						play sound "block.fire.extinguish" with volume 0.4 and pitch 0.7 at the victim
						wait 3 seconds
						clear {t_foeDRTimeout.%victim%}
				if nbt of victim's tool contains "LanceType":
					chance of floor({_drOdds} + 15)%:
						if victim is not on fire:
							chance of 50%:
								set {_damageTacticChosen} to true
								play 20 (emerald block break with speed 1) at the victim
								wait 0.6 seconds
								make the victim shoot an arrow at speed 2
								if victim is not on fire:
									chance of 30%:
										play 20 (emerald block break with speed 1) at the victim
										wait 8 ticks
										make the victim shoot an arrow at speed 2
										wait 4 ticks
										make the victim shoot an arrow at speed 2
				#Dagger mobs will rarely fire a hail of daggers at their foe
				else if nbt of victim's tool contains "DaggerType":
					chance of ({_drOdds} + 15)%:
						if victim is not on fire:
							chance of 50%:
								set {_damageTacticChosen} to true
								play 20 (gold block break with speed 1) at the victim
								wait 0.6 seconds
								if victim is alive:
									loop 2 times:
										if victim is alive:
											make the victim shoot an arrow at speed 2.5
											wait 1 ticks
											make the victim shoot an arrow at speed 2.5
											push the last shot projectile left at speed 1
											make the victim shoot an arrow at speed 2.5
											push the last shot projectile right at speed 1
											wait 3 ticks
											push the victim in horizontal direction of the victim at speed -0.25
											push the victim up at speed 0.2
											loop 3 times:
												play 3 (smoke with speed 0) at the victim
												play 3 (cloud with speed 0) at the victim
												wait 1 tick
											play sound "block.gravel.break" with volume 0.4 and pitch 0.5 at the victim
											play sound "block.fire.extinguish" with volume 0.2 and pitch 0.7 at the victim
											wait 10 ticks
				#Only pick these tactics if the previous defensive tactics haven't been chosen
				if {_damageTacticChosen} is not set:
					#Melee mobs will occasionally stinger to close the gap
					if getEntityRange(victim) is 1:
						chance of floor({_drOdds} + 50)%:
							play 20 lapis block break with speed 1 at the victim
							wait 0.6 seconds
							if victim is alive:
								push the victim upwards at speed 0.1
								push the victim in direction of the attacker at speed 1.4
								play 20 (white wool break with speed 1) at the victim
					#Bow/Magic mobs will occasionally side-strafe and fire more rapidly
					else if getEntityRange(victim) is 2:
						chance of floor({_drOdds} + 50)%:
							play 20 (diamond block break with speed 1) at the victim
							wait 0.5 seconds
							if victim is alive:
								play 9 (smoke with speed 0) at the victim
								play 9 (cloud with speed 0) at the victim
								chance of 50%:
									push the victim upwards at speed 0.1
									push the victim left at speed 0.5
								else:
									push the victim upwards at speed 0.1
									push the victim right at speed 0.5									
								wait 0.2 seconds
								make the victim shoot the attacker at speed 1.35
								wait 0.2 seconds
								play 9 (smoke with speed 0) at the victim
								play 9 (cloud with speed 0) at the victim
								if victim is alive:					
									chance of 50%:
										push the victim upwards at speed 0.1
										push the victim right at speed 1
										wait 0.3 seconds
										make the victim shoot the attacker at speed 1.35
									else:
										push the victim upwards at speed 1.2
										push the victim backwards at speed 0.4
										wait 0.2 seconds
										make the victim shoot the attacker at speed 1.35
						
on damage:
	if metadata value "isSPurgia" of the attacker is 1:
		set {_bonusdamage} to getHelmetDamageResistance(victim)
		add getChestplateDamageResistance(victim) to {_bonusdamage}
		add getLeggingsDamageResistance(victim) to {_bonusdamage}
		add getBootsDamageResistance(victim) to {_bonusdamage}
		add {_bonusdamage} to damage
		apply poison 2 to the victim for 3 seconds
	if metadata value "isZPPurgia" of the attacker is 1:
		ignite the victim
	else if metadata value "isZPurgia" of the victim is 1:
		if damage cause is attack:
			ignite the attacker
	if metadata value "isIPurgia" of the victim is 1:
		if victim's health is victim's maximum health:
			while victim is alive:
				spawn 1 vex at the victim
				apply swiftness 3 to the last spawned entity for 15 seconds
				apply strength 3 to the last spawned entity for 15 seconds
				play 40 (ice break with speed 1) at the victim
				wait 5 seconds
	if metadata value "isMimic" of the victim is 1:
		if victim's health is victim's maximum health:
			set the victim's health to (victim's maximum health - 1)
			play 60 (smoke with speed 1) at the victim
			apply resistance 10 to the victim for 2 seconds
			create safe explosion of force 1 at the victim
			set the victim's chestplate to the attacker's chestplate
			set the victim's helmet to the attacker's helmet
			set the victim's leggings to the attacker's leggings
			set the victim's boots to the attacker's boots
			set the victim's tool to the attacker's tool
			set {t_str.%victim%} to floor({str.%attacker%} * 1)
			set {t_def.%victim%} to floor({def.%attacker%} * 1)
			set {t_skl.%victim%} to floor({skl.%attacker%} * 1)
			set {t_spd.%victim%} to floor({spd.%attacker%} * 1)
			set {t_res.%victim%} to floor({res.%attacker%} * 1)
			set {t_mag.%victim%} to floor({mag.%attacker%} * 1)
	if metadata value "isVenomSpider" of the attacker is 1:
		set {_rng} to random integer from 3 to 9
		applyOOCStatusEffect(victim,"Poison",{_rng})
	if metadata value "isMalediction" of the victim is 1:
		if victim's health is less than (victim's maximum health * 0.5):
			set {_mob} to the victim
			spawn {_mob} at the victim
	if metadata value "isEnrapture" of the attacker is 1:
		apply slowness 5 to the victim for 4 seconds
		play 60 (ice break with speed 1) at the victim
	if metadata value "isShadow" of the attacker is 1:
		apply invisibility 1 to the attacker for 7 seconds
		apply swiftness 4 to the attacker for 7 seconds
		apply strength 4 to the attacker for 7 seconds
	else if metadata value "isShadow" of the victim is 1:
		remove invisibility from the attacker
		play 60 (obsidian break with speed 1) at the attacker
		remove swiftness from the attacker
		remove strength from the attacker
		wait 1 second
		apply invisibility 1 to the victim for 7 seconds
		apply swiftness 4 to the victim for 7 seconds
		apply strength 4 to the victim for 7 seconds
	if metadata value "isRegret" of the victim is 1:
		if {t_RegretSpawner.%victim%} is not 1:
			add 1 to {t_RegretSpawner.%victim%}
			chance of 50%:
				spawn 1 zombie at the victim
			else:
				spawn 1 husk at the victim
			set the last spawned entity's health to (last spawned entity's maximum health * 0.5)
			chance of 50%:
				spawn 1 skeleton at the victim
			else:
				spawn 1 stray at the victim
			set the last spawned entity's health to (last spawned entity's maximum health * 0.5)
			play 40 (purple wool break with speed 1) at the victim
			create safe explosion of force 3 at the victim
			kill the victim
	if metadata value "isFury" of the victim is 1:
		if {t_FurySpawner.%victim%} is not 1:
			while victim is alive:
				add 1 to {t_FurySpawner.%victim%}
				spawn 1 blaze at the victim
				set the last spawned entity's health to (last spawned entity's maximum health * 0.25)
				play 10 (mob spawner flames with speed 1) at the victim
				wait 10 seconds
				if victim is not alive:
					exit 1 loop
				if {t_RegretSpawner.%victim%} is more than 4:
					create safe explosion of force 3 at the victim
					kill the victim
					exit 1 loop
	if metadata value "isStalker" of the victim is 1:
		if metadata value "ignoreDeathCondition" of the victim is not 1:
			cancel event
			set metadata value "ignoreDeathCondition" of the victim to 1
			create safe explosion of force 3 at the victim
			wait 10 ticks
			create safe explosion of force 2 at the victim
			wait 5 ticks
			create safe explosion of force 2 at the victim
			kill the victim
	if metadata value "isZPurgia" of the victim is 1:
		if distance between victim and attacker is less than or equal to 3:
			if victim's health is more than (victim's maximum health * 0.7):
				create safe explosion of force 1 at the victim
				wait 1 tick
				push the attacker backwards at speed 2
				apply weakness 1 to the attacker for 4 seconds
				
on spawn of a monster:
	if entity's world is "Salindrum":
		loop all players in radius 50 of the entity:
			if {t_playingBP.%loop-player%} is not set:
				if {sd.dolevelscaling.%loop-player%} is true:
					exit 1 loop
					set {_sd} to true
		if {_sd} is true:
			set {_moblvl} to 1
			add {lvl.%player%} to {_moblvl}
			add ({promoted.%player%} * 15) to {_moblvl}
			set {_mob} to the last spawned entity
			loop {_moblvl} times:
				#add 1 to {t_str.%entity%}
				chance of 80%:
					add 1 to {t_str.%{_mob}%}
				#add 1 to {t_def.%entity%}
				chance of 50%:
					add 1 to {t_def.%{_mob}%}
				#add 1 to {t_spd.%entity%}
				chance of 60%:
					add 1 to {t_spd.%{_mob}%}
				#add 1 to {t_skl.%entity%}
				chance of 70%:
					add 1 to {t_skl.%{_mob}%}
				#add 1 to {t_res.%entity%}
				chance of 50%:
					add 1 to {t_res.%{_mob}%}
				#add 0.5 to the entity's maximum health
				chance of 80%:
					add 0.5 to {_mob}'s maximum health
			set {t_lvl.%{_mob}%} to {_moblvl}
			heal {_mob} by 9999
		
on explode:
	if event-entity is creeper:
		if metadata value "isSkulker" of the entity is 1:
			if metadata value "ignoreDeathCondition" of the entity is not 1:
				cancel event
				create safe explosion of force 1.5 at the entity
				set metadata value "ignoreDeathCondition" of the entity to 1
				kill the entity
		else if metadata value "isStalker" of the entity is 1:
			if metadata value "ignoreDeathCondition" of the entity is not 1:
				cancel event
				create safe explosion of force 3.5 at the entity
				kill the entity
				set metadata value "ignoreDeathCondition" of the entity to 1
		else if metadata value "isCPurgia" of the entity is 1:
			if metadata value "ignoreDeathCondition" of the entity is not 1:
				cancel event
				kill the entity

on death:
	#if victim is player:
	#	if victim's world is "Salindrum":
	#		if {t_playingBP.%victim%} is not set:
	#			cancel event
	#			heal the victim by victim's maximum health
	#			teleport the victim to {sd.%{sdroom.%victim%}%}
	#			loop all monsters in radius 50 of the victim:
	#				kill loop-monster
	#		if {t_playingQA.%victim%} is not set:
	#			cancel event
	#			heal the victim by victim's maximum health
	#			teleport the victim to {sd.%{sdroom.%victim%}%}
	#			loop all monsters in radius 50 of the victim:
	#				kill loop-monster
	if metadata value "isFury" of the victim is 1:
		create safe explosion of force 1.5 at the victim
		play 60 (smoke with speed 1) at the victim
		play 2 (explosion with speed 1) at the victim
	else if metadata value "isCPurgia" of the victim is 1:
		if metadata value "ignoreDeathCondition" of the victim is not 1:
			loop all players in radius 8 of the victim:
				send "boom" to loop-player
				apply slowness 3 to the loop-entity for 10 seconds
				apply weakness 3 to the loop-entity for 10 seconds
				apply mining fatigue 3 to the loop-entity for 10 seconds
				apply hunger 2 to the loop-entity for 10 seconds
			cancel event
			create safe explosion of force 3 at the entity
			set metadata value "ignoreDeathCondition" of the victim to 1
			kill the entity