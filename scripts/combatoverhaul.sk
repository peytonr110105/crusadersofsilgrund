#Helmet Damage Resistance imported from weaponskills.sk
function helmType(PLR: player) :: number:
	if {_PLR} is wearing a leather helmet:
		return 0.25
	else if {_PLR} is wearing a chain helmet:
		return 0.55
	else if {_PLR} is wearing an iron helmet:
		return 0.75
	else if {_PLR} is wearing a gold helmet:
		return 0.25
	else if {_PLR} is wearing a diamond helmet:
		return 0.8

#Perform a Helm Splitter by attacking an enemy while sneaking, while airbourne.
on damage:
	if attacker is player:
		if ground state of the attacker is false:
			if attacker is sneaking:
				add 1.5 to damage
				add helmType(victim) to damage
				push the attacker downwards at speed 2
				push the victim downwards at speed 2
				play sound "entity.generic.explode" with volume 1 and pitch 1.5 at the attacker for the attacker
				set {inHangtime.%attacker%} to true

#Players can stall themselves in the air by attacking.
on left click:
	if ground state of the player is false:
		if player's tool is not air:
			if {attackHangtimeCount.%player%} is more than 0:
				if {inHangtime.%player%} is not true:
					subtract 1 from {attackHangtimeCount.%player%}
					send "<bold><italics>Air Hang: %{attackHangtimeCount.%player%}%"
					push the player in horizontal direction of the player at speed 0.03
					loop 4 times:
						set {_magnitude} to (y of player's velocity)
						#Cap hangtime boost if it exceeds a specific amount, and take absolute value.
						if {_magnitude} is less than 0:
							set {_magnitude} to {_magnitude} * -1.05
						else:
							set {_magnitude} to 0.05
						push the player upwards at speed {_magnitude}
						wait 1 tick
						set {inHangtime.%player%} to true
					clear {inHangtime.%player%}

				
#Restore aerial hangtime when on the ground (Adjust polling time to adjust balance between responsiveness and script performance.)
every 0.25 seconds:
	loop all players:
		if ground state of loop-player is true:
			set {attackHangtimeCount.%loop-player%} to 5
			clear {inHangtime.%loop-player%}
	
#Players can transfer more momentum into the air.
on jump:
	if player is sprinting:
		set {_magnitude} to sqrt(((x of player's velocity ^ 2) + (y of player's velocity ^ 2) + (z of player's velocity ^ 2))) * 3.5
		#Cap maximum speed if it exceeds a specific amount, and take absolute value.
		if {_magnitude} is less than 0:
			set {_magnitude} to {_magnitude} * -1
		else if {_magnitude} is more than 0.07:
			set {_magnitude} to 0.07
		push the player in horizontal direction of the player at speed {_magnitude}
		set {_magnitude} to {_magnitude} / 1.15
		wait 1 tick
		#ZOOM!
		while {_magnitude} is more than 0.005:
			add 0.01 to {_decay}
			push the player in horizontal direction of the player at speed {_magnitude}
			set {_magnitude} to {_magnitude} / (1.05 + {_decay})
			#send "%{_magnitude}%"
			if ground state of the player is true:
				wait 1 tick
				exit 1 loop
			wait 1 tick
			
			
#Gold/Green Herb Pickup Behavior
on pick up:
	if the name of the event-item is "<light green><bold>Sacred Herb":
		if player's health is less than player's maximum health:
			cancel event
			delete event-entity
			play sound "entity.player.swim" with volume 2 and pitch 1.5 at the player for the player
			play sound "item.bucket.fill" with volume 2 and pitch 1.3 at the player for the player
			play 30 (light green wool break with speed 1) at the player
			heal the player by 3 hearts
			send "<light green>Consumed a Sacred Herb and restored 3 HP."
	else if the name of the event-item is "<light yellow><bold>Golden Herb":
		if player's health is less than player's maximum health:
			cancel event
			delete event-entity
			play sound "block.enchantment_table.use" with volume 3 and pitch 2 at the player for the player
			play sound "entity.player.swim" with volume 1 and pitch 1.5 at the player for the player
			play 30 (gold block break with speed 1) at the player
			heal the player by 6 hearts
			send "<light yellow>Consumed a Golden Herb and restored 12 HP."
			
#Gold/Green Herb Use Behavior
on right click:
	if the name of the player's tool is "<light green><bold>Sacred Herb":
		if player's health is less than player's maximum health:
			remove 1 of tool from tool
			play sound "entity.player.swim" with volume 2 and pitch 1.5 at the player for the player
			play sound "item.bucket.fill" with volume 2 and pitch 1.3 at the player for the player
			play 30 (light green wool break with speed 1) at the player
			heal the player by 6 hearts
			send "<light green>Consumed a Sacred Herb and restored 6 HP."
		else:
			send "<light red>Using this wouldn't benefit you right now."
	if the name of the player's tool is "<light yellow><bold>Golden Herb":
		if player's health is less than player's maximum health:
			remove 1 of tool from tool
			play sound "block.enchantment_table.use" with volume 3 and pitch 2 at the player for the player
			play sound "entity.player.swim" with volume 1 and pitch 1.5 at the player for the player
			play 30 (gold block break with speed 1) at the player
			heal the player by 12 hearts
			send "<light yellow>Consumed a Golden Herb and restored 12 HP."
		else:
			send "<light red>Using this wouldn't benefit you right now."
		
#Fancy particle FX on mob death		
on death:
	play 40 (redstone block break with speed 1) at the victim
	#Small chance of Green/Gold Herbs being dropped on mob death.
	chance of 75%:
		play 30 (light green wool break with speed 1) at the victim
		drop 1 green dye named "<light green><bold>Sacred Herb" with lore "<white>A medicinal herb used to heal wounds.","<white>Right-click to restore 6 hearts.","<white>Can also be picked up off the ground to restore 3 hearts if wounded." at the victim
	chance of 10%:
		play 30 (gold block break with speed 1) at the victim
		drop 1 yellow dye named "<light yellow><bold>Golden Herb" with lore "<white>Powerful medicinal herb with extreme healing power.","<white>Right-click to restore 12 hearts.","<white>Can also be picked up off the ground to restore 6 hearts if wounded." at the victim