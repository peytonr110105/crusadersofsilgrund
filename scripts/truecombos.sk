#Combo System that hooks into Skills.sk and Classes.sk to provide a Devil May Cry / Bayonetta style combo meter.

#Skill Slot Check Function
function checkSlots(PLR: player , skill: text) :: number:
	if {skillslot1.%{_PLR}%} contains "%{_skill}%":
		return 1
	else if {skillslot2.%{_PLR}%} contains "%{_skill}%":
		return 1
	else if {skillslot3.%{_PLR}%} contains "%{_skill}%":
		return 1
	else if {skillslot4.%{_PLR}%} contains "%{_skill}%":
		return 1
	else if {skillslot5.%{_PLR}%} contains "%{_skill}%":
		return 1
	else:
		return 0

command /combo [<text="toggle">]:
	description: Toggle combo system.
	usage: /combo
	executable by: players
	trigger:
		if {combosenabled.%player%} is set:
			clear {combosenabled.%player%}
			send "<gold><bold>[Combo System]<reset> Disabled combo system."
		else:
			set {combosenabled.%player%} to 1
			send "<gold><bold>[Combo System]<reset> Enabled combo system. Consecutive hits and stylish actions like dodge rolls increase your combo score. Raise your score to earn a damage multiplier!"

on join:
	clear {comboscore.%player%}
	set {combograde.%player%} to 0
	set {combomult.%player%} to 1
	set {combodecay.%player%} to 0
	clear {comboscale.%player%}
	clear {comboscalar.%player%}

every 0.55 seconds:
	loop all players:
		if {combosenabled.%loop-player%} is 1:
			if {comboscore.%loop-player%} is more than 0:
				if {combograde.%loop-player%} is 0:
					if {comboscalar.%loop-player%} is more than 3:
						set {comboscale.%loop-player%} to floor({comboscore.%loop-player%} / 5)
						set {combograde.%loop-player%} to 1
						set {comboscalar.%loop-player%} to 0
						set {combobank.%loop-player%} to 0
				else:
					subtract ({comboscore.%loop-player%} / ({comboscale.%loop-player%} + {combograde.%loop-player%})) from {comboscore.%loop-player%}
					if {comboscore.%loop-player%} is less than (({combograde.%loop-player%} * 2.5) * {comboscale.%loop-player%}):
						subtract 1 from {combograde.%loop-player%}
						if {combograde.%loop-player%} is 7:
							send title "<red><bold>Smokin' Sick Style!!!" with subtitle "<gold><bold>[3x Damage Multiplier!]" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
							set {combomult.%loop-player%} to 3
						else if {combograde.%loop-player%} is 6:
							send title "<light red><bold>Sensational Style!!" with subtitle "<bold>[2.5x Damage Multiplier!]" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
							set {combomult.%loop-player%} to 2.5
						else if {combograde.%loop-player%} is 5:
							send title "<light red>Showtime!!" with subtitle "<white>[2x Damage Multiplier!]" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
							set {combomult.%loop-player%} to 2
						else if {combograde.%loop-player%} is 4:
							set {combomult.%loop-player%} to 1.75
							send title "<gold>Are you ready?!" with subtitle "<bold>[1.75x Damage Multiplier!]" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						else if {combograde.%loop-player%} is 3:
							set {combomult.%loop-player%} to 1.5
							send title "<light yellow>Bangin'!" with subtitle "<bold>[1.5x Damage Multiplier!]" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						else if {combograde.%loop-player%} is 2:
							set {combomult.%loop-player%} to 1.25
							send title "<bold>Cool!" with subtitle "<bold>[1.25x Damage Multiplier!]" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						else if {combograde.%loop-player%} is 1:
							set {combomult.%loop-player%} to 1
							send title "<light gray>Don't Worry" with subtitle "" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						else if {combograde.%loop-player%} is set:
							send title "<light red>Combo Dropped..." with subtitle "" to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
							clear {comboscore.%loop-player%}
							set {comboscalar.%loop-player%} to 0
							clear {comboscale.%loop-player%}
							set {combograde.%loop-player%} to 0
							wait 3.5 seconds
							send title "<yellow>Total Style Points: %ceil({combobank.%loop-player%})%" with subtitle "+$%ceil({combobank.%loop-player%} ^ 0.725)% earned." to loop-player for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
							add ceil({combobank.%loop-player%} ^ 0.725) to loop-player's balance
							set {combobank.%loop-player%} to 0
					send action bar "<bold>Style Points: <reset><gold>%{comboscore.%loop-player%}%<reset><bold> | Points for next Stylish Rank:<reset><gold> %(((({combograde.%loop-player%} + 1) * 3.5) * {comboscale.%loop-player%}))%<reset><bold> | Points To Maintain: <reset><gold>%((({combograde.%loop-player%} * 2.5) * {comboscale.%loop-player%}))%" to loop-player
			else:
				clear {comboscore.%loop-player%}
				set {comboscalar.%loop-player%} to 0
				clear {comboscale.%loop-player%}
				set {combograde.%loop-player%} to 0
				
on death of a player:
	set {combo.%player%} to 0
	set {combograde.%player%} to 0
	set {combomult.%player%} to 0

on damage:
	if {combosenabled.%victim%} is 1:
		if {combograde.%victim%} is more than 0:
			if damage cause is attack:
				subtract (damage * 2) from {comboscore.%victim%}
			else if projectile exists:
				subtract (damage * 2) from {comboscore.%victim%}
			send action bar "<bold>Style Points: <reset><gold>%{comboscore.%victim%}%<reset><bold> | Points for next Stylish Rank:<reset><gold> %(((({combograde.%victim%} + 1) * 3.5) * {comboscale.%victim%}))%<reset><bold> | Points To Maintain: <reset><gold>%((({combograde.%victim%} * 2.5) * {comboscale.%victim%}))%" to victim
	if {combosenabled.%attacker%} is 1:
		if {combograde.%attacker%} is 0:
			add damage to {comboscore.%attacker%}
			add 1 to {comboscalar.%attacker%}
		else:
			add damage to {comboscore.%attacker%}
			if the 6th line of the lore of the attacker's tool contains "Stylish":
				add damage / 2 to {comboscore.%attacker%}
			#subtract ({comboscale.%attacker%} / 2) from {comboscore.%attacker%}
			set damage to (damage * {combomult.%attacker%})
			#if {comboscore.%attacker%} is more than {combobank.%attacker%}:
			#	set {combobank.%attacker%} to {comboscore.%attacker%}
			add damage to {combobank.%attacker%}
			if {comboscore.%attacker%} is more than ((({combograde.%attacker%} + 1) * 3.5) * {comboscale.%attacker%}):
				if {combograde.%attacker%} is less than 7:
					add 1 to {combograde.%attacker%}
					if {combograde.%attacker%} is 7:
						send title "<red><bold>Smokin' Sick Style!!!" with subtitle "<gold><bold>[3x Damage Multiplier!]" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						set {combomult.%attacker%} to 3
						spawn 2 fireworks at the attacker
						play sound "entity.enderdragon.growl" with volume 0.5 and pitch 1 at attacker for attacker
						apply resistance 5 to the attacker for 2 seconds
						create safe explosion of force 1 at attacker
					if {combograde.%attacker%} is 6:
						send title "<light red><bold>Sensational Style!!" with subtitle "<bold>[2.5x Damage Multiplier!]" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						set {combomult.%attacker%} to 2.5
						spawn 1 firework at the attacker
						play sound "entity.player.levelup" with volume 1 and pitch 0.7 at attacker for attacker
					else if {combograde.%attacker%} is 5:
						send title "<light red>Showtime!!" with subtitle "<white>[2x Damage Multiplier!]" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						set {combomult.%attacker%} to 2
						spawn 1 firework at the attacker
						play sound "entity.experience_orb.pickup" with volume 1 and pitch 1 at attacker for attacker
					else if {combograde.%attacker%} is 4:
						set {combomult.%attacker%} to 1.75
						send title "<gold>Are you ready?!" with subtitle "<bold>[1.75x Damage Multiplier!]" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						spawn 1 firework at the attacker
						play sound "entity.generic.explode" with volume 1 and pitch 1 at attacker for attacker
					else if {combograde.%attacker%} is 3:
						set {combomult.%attacker%} to 1.5
						send title "<light yellow>Bangin'!" with subtitle "<bold>[1.5x Damage Multiplier!]" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						spawn 1 firework at the attacker
						play sound "block.anvil.place" with volume 1 and pitch 0.75 at attacker for attacker
					else if {combograde.%attacker%} is 2:
						set {combomult.%attacker%} to 1.25
						send title "<bold>Cool!" with subtitle "<bold>[1.25x Damage Multiplier!]" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						spawn 1 firework at the attacker
					else if {combograde.%attacker%} is 1:
						set {combomult.%attacker%} to 1
						send title "<light gray>Don't Worry" with subtitle "" to attacker for 3 seconds with fade in 0.25 seconds and fade out 0.25 seconds
						play sound "entity.experience_orb.pickup" with volume 1 and pitch 0.5 at attacker for attacker
			send action bar "<bold>Style Points: <reset><gold>%{comboscore.%attacker%}%<reset><bold> | Points for next Stylish Rank:<reset><gold> %(((({combograde.%attacker%} + 1) * 3.5) * {comboscale.%attacker%}))%<reset><bold> | Points To Maintain: <reset><gold>%((({combograde.%attacker%} * 2.5) * {comboscale.%attacker%}))%" to attacker